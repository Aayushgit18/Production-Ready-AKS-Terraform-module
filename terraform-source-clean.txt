
========== ./bootstrap/main.tf ==========
provider "azurerm" {
  features {}
  subscription_id = "4a564de4-a49a-46bd-9205-3b7ba10c540d"

}

resource "azurerm_resource_group" "tfstate" {
  name     = var.rg_name
  location = var.location
}

resource "azurerm_storage_account" "tfstate" {
  name                     = var.storage_account_name
  resource_group_name      = azurerm_resource_group.tfstate.name
  location                 = var.location
  account_tier             = "Standard"
  account_replication_type = "LRS"
  min_tls_version          = "TLS1_2"
}

resource "azurerm_storage_container" "tfstate" {
  name                  = var.container_name
  storage_account_name  = azurerm_storage_account.tfstate.name
  container_access_type = "private"
}

========== ./bootstrap/outputs.tf ==========
output "storage_account_name" {
  value = azurerm_storage_account.tfstate.name
}

output "resource_group_name" {
  value = azurerm_resource_group.tfstate.name
}

========== ./bootstrap/variables.tf ==========
variable "location" {
  default = "eastus"
}

variable "rg_name" {
  default = "tfstate-rg"
}

variable "storage_account_name" {
  description = "Globally unique storage account name"
}

variable "container_name" {
  default = "tfstate"
}

========== ./environments/dev/backend.tf ==========
terraform {
  backend "azurerm" {
    resource_group_name  = "tfstate-rg"
    storage_account_name = "foraks"
    container_name       = "tfstate"
    key                  = "dev/aks.tfstate"
  }
}

========== ./environments/dev/main.tf ==========
provider "azurerm" {
  features {}
}

module "rg" {
  source   = "../../modules/resource-group"
  name     = var.rg_name
  location = var.location
  tags     = var.tags
}

module "network" {
  source                    = "../../modules/network"
  rg_name                   = module.rg.name
  location                  = var.location
  vnet_name                 = var.vnet_name
  address_space             = var.vnet_cidr
  system_subnet_cidr        = var.system_subnet_cidr
  user_subnet_cidr          = var.user_subnet_cidr
  tags                      = var.tags
}

module "monitoring" {
  source   = "../../modules/monitoring"
  rg_name  = module.rg.name
  location = var.location
}

module "acr" {
  source    = "../../modules/acr"
  rg_name   = module.rg.name
  location  = var.location
  name      = var.acr_name
  tags      = var.tags
}

module "aks" {
  source              = "../../modules/aks"
  rg_name             = module.rg.name
  location            = var.location
  name                = var.aks_name
  law_id              = module.monitoring.law_id
  acr_id              = module.acr.acr_id
  tags                = var.tags
}

module "key_vault" {
  source    = "../../modules/key-vault"
  name      = var.key_vault_name
  location  = var.location
  rg_name   = module.rg.name
  tenant_id = var.tenant_id
  tags      = var.tags
}

========== ./environments/dev/outputs.tf ==========
# AKS Outputs

output "aks_cluster_name" {
  description = "AKS cluster name"
  value       = module.aks.cluster_name
}

output "aks_resource_group" {
  description = "AKS resource group name"
  value       = module.rg.name
}

output "aks_kubeconfig" {
  description = "Raw kubeconfig for AKS cluster"
  value       = module.aks.kubeconfig
  sensitive   = true
}

output "aks_oidc_issuer_url" {
  description = "OIDC issuer URL for workload identity"
  value       = module.aks.oidc_issuer_url
}

# ACR Outputs

output "acr_id" {
  description = "Azure Container Registry ID"
  value       = module.acr.acr_id
}

output "acr_login_server" {
  description = "ACR login server URL"
  value       = module.acr.login_server
}

# Monitoring Outputs

output "log_analytics_workspace_id" {
  description = "Log Analytics Workspace ID"
  value       = module.monitoring.law_id
}

# Network Outputs


output "vnet_name" {
  description = "Virtual Network name"
  value       = var.vnet_name
}

output "system_subnet_id" {
  description = "AKS system node subnet ID"
  value       = module.network.system_subnet_id
}

output "user_subnet_id" {
  description = "AKS user node subnet ID"
  value       = module.network.user_subnet_id
}





========== ./environments/dev/terraform.tfvars ==========
location           = "eastus"
rg_name            = "aks-dev-rg"
aks_name           = "aks-dev"
acr_name           = "acrdevayu2026512"
vnet_name          = "aks-dev-vnet"
vnet_cidr          = ["10.0.0.0/8"]
system_subnet_cidr = "10.1.0.0/16"
user_subnet_cidr   = "10.2.0.0/16"
tags = {
  environment = "dev"
  owner       = "platform"
}
key_vault_name = "kv-aks-dev-aayush1290"
tenant_id      = "aac42910-b03a-44c1-b372-7cee5c67a2d5"

========== ./environments/dev/variables.tf ==========
variable "location" { type = string }
variable "rg_name" { type = string }
variable "aks_name" { type = string }
variable "acr_name" { type = string }
variable "vnet_name" { type = string }
variable "vnet_cidr" { type = list(string) }
variable "system_subnet_cidr" { type = string }
variable "user_subnet_cidr" { type = string }
variable "tags" { type = map(string) }
variable "key_vault_name" { type = string }
variable "tenant_id" { type = string }

========== ./environments/dev/versions.tf ==========
terraform {
  required_version = ">= 1.4.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.90"
    }
  }
}

========== ./environments/prod/backend.tf ==========
terraform {
  backend "azurerm" {
    resource_group_name  = "tfstate-rg"
    storage_account_name = "REPLACE_WITH_YOUR_STORAGE_ACCOUNT_NAME"
    container_name       = "tfstate"
    key                  = "prod/aks.tfstate"
  }
}

========== ./environments/prod/main.tf ==========
provider "azurerm" {
  features {}
}

module "rg" {
  source   = "../../modules/resource-group"
  name     = var.rg_name
  location = var.location
  tags     = var.tags
}

module "network" {
  source              = "../../modules/network"
  rg_name             = module.rg.name
  location            = var.location
  vnet_name           = var.vnet_name
  address_space       = var.vnet_cidr
  system_subnet_cidr  = var.system_subnet_cidr
  user_subnet_cidr    = var.user_subnet_cidr
  tags                = var.tags
}

module "monitoring" {
  source   = "../../modules/monitoring"
  rg_name  = module.rg.name
  location = var.location
}

module "acr" {
  source   = "../../modules/acr"
  rg_name  = module.rg.name
  location = var.location
  name     = var.acr_name
  tags     = var.tags
}

module "aks" {
  source             = "../../modules/aks"
  rg_name            = module.rg.name
  location           = var.location
  name               = var.aks_name
  law_id             = module.monitoring.law_id
  acr_id             = module.acr.acr_id
  tags               = var.tags
}

module "key_vault" {
  source   = "../../modules/key-vault"
  name     = var.key_vault_name
  location = var.location
  rg_name  = module.rg.name
  tenant_id = var.tenant_id
  tags     = var.tags
}

========== ./environments/prod/outputs.tf ==========
output "aks_cluster_name" {
  value = module.aks.cluster_name
}

output "aks_resource_group" {
  value = module.rg.name
}

output "aks_kubeconfig" {
  value     = module.aks.kubeconfig
  sensitive = true
}

output "acr_login_server" {
  value = module.acr.login_server
}

========== ./environments/prod/terraform.tfvars ==========
location            = "eastus"
rg_name             = "aks-prod-rg"
aks_name            = "aks-prod"
acr_name            = "acrprodayu2026512"
vnet_name           = "aks-prod-vnet"
vnet_cidr           = ["10.10.0.0/16"]
system_subnet_cidr  = "10.10.1.0/24"
user_subnet_cidr    = "10.10.2.0/24"

tags = {
  environment = "prod"
  owner       = "platform"
  costcenter  = "production"
}
key_vault_name = "kv-aks-prod"
tenant_id      = "<YOUR_TENANT_ID>"

========== ./environments/prod/variables.tf ==========
variable "location" { type = string }
variable "rg_name" { type = string }
variable "aks_name" { type = string }
variable "acr_name" { type = string }
variable "vnet_name" { type = string }
variable "vnet_cidr" { type = list(string) }
variable "system_subnet_cidr" { type = string }
variable "user_subnet_cidr" { type = string }
variable "tags" { type = map(string) }
variable "key_vault_name" { type = string }
variable "tenant_id" { type = string }

========== ./environments/prod/versions.tf ==========
terraform {
  required_version = ">= 1.4.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.90"
    }
  }
}

========== ./modules/acr/main.tf ==========
resource "azurerm_container_registry" "this" {
  name                          = var.name
  resource_group_name           = var.rg_name
  location                      = var.location
  sku                           = "Premium"
  admin_enabled                 = false
  public_network_access_enabled = true
  tags                          = var.tags
}

========== ./modules/acr/outputs.tf ==========
output "acr_id" {
  value = azurerm_container_registry.this.id
}

output "login_server" {
  value = azurerm_container_registry.this.login_server
}

========== ./modules/acr/variables.tf ==========
variable "rg_name" { type = string }
variable "location" { type = string }
variable "name" { type = string }
variable "tags" { type = map(string) }

========== ./modules/aks/main.tf ==========
resource "azurerm_user_assigned_identity" "aks" {
  name                = "${var.name}-uami"
  location            = var.location
  resource_group_name = var.rg_name
}

resource "azurerm_kubernetes_cluster" "this" {
  name                = var.name
  location            = var.location
  resource_group_name = var.rg_name
  dns_prefix          = var.name

  oidc_issuer_enabled       = true
  workload_identity_enabled = true
  automatic_channel_upgrade = "stable"
  azure_policy_enabled      = true

  identity {
    type         = "UserAssigned"
    identity_ids = [azurerm_user_assigned_identity.aks.id]
  }

  role_based_access_control_enabled = true
  azure_active_directory_role_based_access_control {
    managed = true
  }

  oms_agent {
    log_analytics_workspace_id = var.law_id
  }

  default_node_pool {
    name                = "system"
    vm_size             = "Standard_D2s_v3"
    enable_auto_scaling = true
    min_count           = 1
    max_count           = 2
  }

  network_profile {
    network_plugin    = "azure"
    network_policy    = "azure"
    load_balancer_sku = "standard"
    service_cidr      = "172.16.0.0/16"
    dns_service_ip    = "172.16.0.10"
  }
}

resource "azurerm_kubernetes_cluster_node_pool" "user" {
  name                  = "user"
  kubernetes_cluster_id = azurerm_kubernetes_cluster.this.id
  vm_size               = "Standard_D2s_v3"
  enable_auto_scaling   = true
  min_count             = 1
  max_count             = 2
  mode                  = "User"

  node_labels = {
    workload = "apps"
  }
}

resource "azurerm_role_assignment" "acr_pull" {
  scope                = var.acr_id
  role_definition_name = "AcrPull"
  principal_id         = azurerm_kubernetes_cluster.this.kubelet_identity[0].object_id
}

========== ./modules/aks/outputs.tf ==========
output "cluster_name" {
  description = "AKS cluster name"
  value       = azurerm_kubernetes_cluster.this.name
}

output "kubeconfig" {
  description = "Raw kubeconfig"
  value       = azurerm_kubernetes_cluster.this.kube_config_raw
  sensitive   = true
}

output "oidc_issuer_url" {
  description = "OIDC issuer URL"
  value       = azurerm_kubernetes_cluster.this.oidc_issuer_url
}
output "aks_identity_principal_id" { value = azurerm_user_assigned_identity.aks.principal_id }

========== ./modules/aks/variables.tf ==========
variable "rg_name" { type = string }
variable "location" { type = string }
variable "name" { type = string }
variable "system_subnet_id" { type = string }
variable "user_subnet_id" { type = string }
variable "law_id" { type = string }
variable "acr_id" { type = string }
variable "tags" { type = map(string) }

========== ./modules/key-vault/main.tf ==========
resource "azurerm_key_vault" "this" {
  name                          = var.name
  location                      = var.location
  resource_group_name           = var.rg_name
  tenant_id                     = var.tenant_id
  sku_name                      = "standard"
  enable_rbac_authorization     = true
  public_network_access_enabled = true
  tags                          = var.tags
}

========== ./modules/key-vault/outputs.tf ==========
output "id" {
  value = azurerm_key_vault.this.id
}

output "name" {
  value = azurerm_key_vault.this.name
}

========== ./modules/key-vault/variables.tf ==========
variable "name" { type = string }
variable "location" { type = string }
variable "rg_name" { type = string }
variable "tenant_id" { type = string }
variable "tags" { type = map(string) }

========== ./modules/monitoring/main.tf ==========
resource "azurerm_log_analytics_workspace" "this" {
  name = "${var.rg_name}-law"
  location            = var.location
  resource_group_name = var.rg_name
  sku                 = "PerGB2018"
  retention_in_days   = 30
}

========== ./modules/monitoring/outputs.tf ==========
output "law_id" { value = azurerm_log_analytics_workspace.this.id }

========== ./modules/monitoring/variables.tf ==========
variable "rg_name" { type = string }
variable "location" { type = string }

========== ./modules/network/main.tf ==========
resource "azurerm_virtual_network" "this" {
  name                = var.vnet_name
  location            = var.location
  resource_group_name = var.rg_name
  address_space       = var.address_space
  tags                = var.tags
}

resource "azurerm_subnet" "system" {
  name                 = "aks-system-subnet"
  resource_group_name  = var.rg_name
  virtual_network_name = azurerm_virtual_network.this.name
  address_prefixes     = [var.system_subnet_cidr]
}

resource "azurerm_subnet" "user" {
  name                 = "aks-user-subnet"
  resource_group_name  = var.rg_name
  virtual_network_name = azurerm_virtual_network.this.name
  address_prefixes     = [var.user_subnet_cidr]
}

resource "azurerm_network_security_group" "aks" {
  name                = "${var.vnet_name}-nsg"
  location            = var.location
  resource_group_name = var.rg_name
}

resource "azurerm_subnet_network_security_group_association" "system" {
  subnet_id                 = azurerm_subnet.system.id
  network_security_group_id = azurerm_network_security_group.aks.id
}

resource "azurerm_subnet_network_security_group_association" "user" {
  subnet_id                 = azurerm_subnet.user.id
  network_security_group_id = azurerm_network_security_group.aks.id
}

========== ./modules/network/outputs.tf ==========
output "system_subnet_id" { value = azurerm_subnet.system.id }
output "user_subnet_id" { value = azurerm_subnet.user.id }
output "vnet_id" { value = azurerm_virtual_network.this.id }

========== ./modules/network/variables.tf ==========
variable "rg_name" { type = string }
variable "location" { type = string }
variable "vnet_name" { type = string }
variable "address_space" { type = list(string) }
variable "system_subnet_cidr" { type = string }
variable "user_subnet_cidr" { type = string }
variable "tags" { type = map(string) }

========== ./modules/resource-group/main.tf ==========
resource "azurerm_resource_group" "this" {
  name     = var.name
  location = var.location
  tags     = var.tags
}

========== ./modules/resource-group/outputs.tf ==========
output "name" { value = azurerm_resource_group.this.name }

========== ./modules/resource-group/variables.tf ==========
variable "name" { type = string }
variable "location" { type = string }
variable "tags" { type = map(string) }

========== ./versions.tf ==========
terraform {
  required_version = ">= 1.4.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.90"
    }
  }
}
